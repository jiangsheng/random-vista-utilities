%KIDS ;GFT/MSC  KIDS UTILITIES;23OCT2014
 ;;1.6;MSC;**MSC,1600**;
 ; ; Written by George Timson
 ;
 ; Please send back any bugs to hardhats@googlegroups.com
 ;
 ;
 ;
COMPARE ;COMPARE TWO KIDS FILES
 D DT^DICRW
 K ^TMP("%KIDS",$J)
 N %KIDS,POP,%ZIS,OUT,OUTIOM,DIR,FIRST,XPDUL,V,XPDLR,Y
 S XPDLR=1 W "FIRST FILE'S " D  Q:'$D(@%KIDS(1))  S XPDLR=2 W "SECOND FILE'S " D  G END:'$D(@%KIDS(2))
 .N RSA,DIR,PATH,SPEC,LIST
 .S %KIDS(XPDLR)=$NA(^TMP("%KIDS",$J,XPDLR_.1)) ;WE STORE THE BUILDS IN 1.1 AND 2.1
 .S PATH=$$PATH^% Q:PATH=""
 .S SPEC("*.KID")="" Q:'$$LIST^%ZISH(PATH,"SPEC","LIST")
 .S LIST="",Y=0,RSA="" F  S LIST=$O(LIST(LIST)) Q:LIST=""  S Y=Y+1,RSA=RSA_Y_":"_LIST_";"
 .K LIST,^TMP("%",$J)
 .S DIR(0)="SA^"_RSA,DIR("A")="KID file #"_XPDLR_": " D ^DIR W !!
 .Q:'Y  S LIST=$P($P(DIR(0),+Y_":",2),";") D OPEN^%ZISH("GFT",PATH,LIST,"R") Q:POP  Q:'$$FTG^%ZISH(PATH,LIST,$NA(^TMP("%KIDS",$J,XPDLR,1,0)),4)
 .I XPDLR=1 S FIRST=PATH_LIST
 .K Y F X=3:1:9 I $G(^TMP("%KIDS",$J,XPDLR,X,0))="**INSTALL NAME**" S Y=X Q
 .I '$D(Y) W !,"BAD FILE" Q
 .S Y=Y+1
 .F  S Y=Y+1,X=$G(^TMP("%KIDS",$J,XPDLR,Y,0)) Q:X="**END**"!'$L(X)  S Y=Y+1,V=$G(^TMP("%KIDS",$J,XPDLR,Y,0)),X="@%KIDS(XPDLR)@("_X,@X=V
 S %ZIS("A")="Output Device: " D ^%ZIS Q:POP  S OUT=IO,OUTIOM=IOM S $P(XPDUL,"-",OUTIOM+1)="" U IO W @IOF,!
 N Y1 F X=3,1,2 D  D SBS(Y1,Y)
 .F I=1,2 D
 ..S Y=$O(@%KIDS(I)@("BLD",0)) I Y S Y(I)=$NA(^(Y,1))
 ..S Y=^TMP("%KIDS",$J,I,X,0) S:X=1 Y=$P(Y,"KIDS Distribution ",2) S:X=3 Y=$P($P(Y,"**KIDS**:",2),U) S:I=1 Y1=Y
 W !,XPDUL D CPL^%(Y(1),Y(2),"BUILD DESCRIPTION")
 D ROU,FIA,KRN
 D ^%ZISC
END K ^TMP("%KIDS",$J)
Q Q
 ;
 ;
 ;
SBS(S1,S2) ;SIDE BY SIDE PRINT
 N K,C,X1,X2 S C=OUTIOM-2/2\1
 F K=1:1 W !,$E(S1,1,C-5),?C+1,$E(S2,1,C-5) S S1=$E(S1,C-4,255),S2=$E(S2,C-4,255) I $L(S1)+$L(S2)=0 Q
 Q
 ;
 ;
 ;compare routines
ROU N XPDI,DITCPT
 S XPDI=""
 F  S XPDI=$O(@%KIDS(1)@("RTN",XPDI)) Q:XPDI=""  S X=$G(^(XPDI)) D CPR
 F  S XPDI=$O(@%KIDS(2)@("RTN",XPDI)) Q:XPDI=""  S X=$G(^(XPDI)) I '$D(@%KIDS(1)@("RTN",XPDI)) D CPR
 Q
 ;
CPR I X W:X=1 !!,"DELETE Routine: ",XPDI,! Q
 D MISSING("RTN",XPDI)
 D CPL^%(Y(1),Y(2),XPDI_" ROUTINE")
 Q
 ;
MISSING(Z,XPDI) N X
 F X=1,2 S Y(X)=$NA(@%KIDS(X)@(Z,XPDI))
 I '$D(@Y(1)) S D=$D(@Y(2)) K @Y(2) S @Y(1)@(1)="Missing",@Y(2)@(1)=" "
 E  I '$D(@Y(2)) K @Y(1) S @Y(1)@(1)=" ",@Y(2)@(1)="Missing"
 Q  ;$T DEFINED
 ;
FIA N DIC,XPDFIL1,XPDFIL,XPDFILO,XPDFILS,XPDS,XPDS0,XPDX,XPDX0,XPDY,XPDY1,XPDZ,XPDZ1,XPDZ2,X,Y
 S XPDFIL=0
 F  S XPDFIL=$O(@%KIDS(1)@("FIA",XPDFIL)) Q:'XPDFIL  S XPDFIL1(XPDFIL)="" D CPG(1)
 F  S XPDFIL=$O(@%KIDS(2)@("FIA",XPDFIL)) Q:'XPDFIL  I '$D(XPDFIL1(XPDFIL)) D CPG(2)
 W ! Q
 ;
CPG(XPDLR) S XPDZ1=^(XPDFIL,0),XPDFILO=^(0,1)
 D MISSING("FIA",XPDFIL)
 S XPDZ="^TMP(""%KIDS"",$J,1.1",XPDZ2="^TMP(""%KIDS"",$J,2.1",XPDY=XPDZ_",""^DIC"","_XPDFIL_","_XPDFIL_",0",XPDX=XPDY_")"
 S XPDY=XPDY_",",XPDY1=XPDZ2,XPDY=XPDZ_",""^DIC"","_XPDFIL_","_XPDFIL_",0",XPDS=XPDY1_")",XPDY1=XPDY1_","
 D COMPAR(1)
 S XPDFILS=0 F  S XPDFILS=$O(^TMP("%KIDS",$J,1.1,"^DD",XPDFIL,XPDFILS)) Q:'XPDFILS  D
 .S X=",""^DD"","_XPDFIL_","_XPDFILS,XPDY=XPDZ_X,XPDX=XPDY_")",XPDY=XPDY_","
 .S XPDY1=XPDZ2_X,XPDS=XPDY1_")",XPDY1=XPDY1_","
 .D DASHES,DITCP^%($NA(@XPDX),$NA(@XPDS),0,"1",.DITCPT) ;COMPARE THE DATA DICTIONARIES
DATA S X=$NA(@%KIDS(1)@("DATA",XPDFIL)),Y=$NA(@%KIDS(2)@("DATA",XPDFIL)) I $D(@X)!$D(@Y),'$D(^DD(XPDFIL)) W !,",CANNOT COMPARE ENTRIES IN FILE ",XPDFIL,! Q
 D DASHES,DITCP^%(X,Y,XPDFIL,"1",.DITCPT)
 Q
 ;
 ;
 ;
KRN N XPDFIL,XPDFILDN,XPDI,XPDS,XPDS0,XPDX,XPDX0,XPDY,XPDY1,XPDZ,XPDZ1,Y
 F XPDS=1,2 F XPDFIL=0:0 S XPDFIL=$O(@%KIDS(XPDS)@("KRN",XPDFIL)) Q:'XPDFIL  S @%KIDS(3-XPDS)@("KRN",XPDFIL,0)="" D:'$D(XPDFILDN(XPDFIL))
 .S XPDFILDN(XPDFIL)="" F X=1,2 S Y(X)=$NA(@%KIDS(X)@("KRN",XPDFIL))
 .D BINDEX,DASHES,DITCP^%(Y(1),Y(2),XPDFIL,1,.DITCPT)
 Q
 ;
BINDEX N I,X F I=0:0 S I=$O(@%KIDS(2)@("KRN",XPDFIL,I)) Q:'I  S X=$P(^(I,0),U) I X]"" S @%KIDS(2)@("KRN",XPDFIL,"B",X,I)=""
 Q
 ;
 ;
DASHES K DITCPT S DITCPT(-3)="",DITCPT(-2)=XPDUL
 Q
 ;
 ;
 ;
 ;
 ; ;taken from XTVGC2 routine
COMPAR(XPDFL) N XPDS1,XPDX1
 F  D  Q:XPDX=""&(XPDS="")
 .S XPDX0=$$QU(.XPDX,XPDY),XPDX1=$P(XPDX,XPDY,2)
 .S XPDS0=$$QU(.XPDS,XPDY1),XPDS1=$P(XPDS,XPDY1,2) Q:XPDX=""&(XPDS="")
 .D CHECK
 Q
CHECK N FL,I,X1,X2,S1,S2
CHK1 I XPDX="" D DEL Q
 I XPDS="" W !,"* ADD *   ",XPDY1_XPDX1," = ",XPDX0 Q
 S FL=$S(XPDX1=XPDS1:0,1:1)
 I FL S X1=$E(XPDX1,1,$L(XPDX1)-1),S1=$E(XPDS1,1,$L(XPDS1)-1) D
 .F I=1:1 S X2=$P(X1,",",I),S2=$P(S1,",",I) I X2'=S2 S FL=$S(+X2=X2:$S(+S2'=S2:1,X2<S2:1,1:-1),+S2=S2:-1,S2]X2:1,1:-1) Q
 I FL<0 D DEL S XPDS0=$$QU(.XPDS,XPDY1),XPDS1=$P(XPDS,XPDY1,2) G CHK1
 I FL>0 W !,"* ADD *   ",XPDY1_XPDX1," = ",XPDX0 S XPDX0=$$QU(.XPDX,XPDY),XPDX1=$P(XPDX,XPDY,2) G CHK1
 I XPDX0'=XPDS0 W !,"* OLD *   ",XPDS," = ",XPDS0,!,"* NEW *   ",XPDY1_XPDX1," = ",XPDX0
 Q
QU(X,Y) ;X=gr, Y=root
 Q:$G(X)="" ""
 S X=$Q(@X)
 I X=""!(X'[$G(Y)) S X="" Q ""
 Q @X
DEL Q:XPDFL
 F I=$QL(XPDY1_"0)")+1:2 S S2=$QS(XPDS,I) Q:S2=""  I 'S2 S I=0 Q  ;GFT  IGNORE CROSS-REFERENCES IN EXISTING DATA
 I I W !,"* DEL *   ",XPDS," = ",XPDS0
 Q
 ;
 ;
 ;
 ;
 ;
 ;
 ;
 ;
 ;
FILE ;
 S DIC=1,DIC(0)="AEQFM",DIC("A")="PICK A FILE WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N D,DIF S DIF=D0 F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,4,DIF)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ;
 ;
OPTION ;
 S DIC=19,DIC(0)="AEQFM",DIC("A")="PICK AN OPTION WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N XPDNAME,D S XPDNAME=$P($G(^DIC(19,D0,0)),U) I XPDNAME]"" F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",19,"NM","B",XPDNAME)) S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ;
 ;
FORM ;
 S DIC=.403,DIC(0)="AEQM",DIC("A")="PICK A FORM WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N DISNAME,D S DISNAME=$P($G(^DIST(.403,D0,0)),U)_"    FILE #"_$P($G(^(0)),U,8) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",.403,"NM","B",DISNAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ; 
PRINT ;
 S DIC=.4,DIC(0)="AEQM",DIC("A")="PICK A PRINT TEMPLATE WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N DIPTNAME,D S DIPTNAME=$P($G(^DIPT(D0,0)),U)_"    FILE #"_$P($G(^(0)),U,4) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",.4,"NM","B",DIPTNAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ;
 ;
 ;
SORT ;
 S DIC=.401,DIC(0)="AEQM",DIC("A")="PICK A SORT TEMPLATE WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N DIBTNAME,D S DIBTNAME=$P($G(^DIBT(D0,0)),U)_"    FILE #"_$P($G(^(0)),U,4) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",.401,"NM","B",DIBTNAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ;
 ;
INPUT ;
 S DIC=.402,DIC(0)="AEQM",DIC("A")="PICK AN INPUT TEMPLATE WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N DIENAME,D S DIENAME=$P($G(^DIE(D0,0)),U)_"    FILE #"_$P($G(^(0)),U,4) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",.402,"NM","B",DIENAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
 ;
 ;
 ;
 ;
ROUTINE ;
 S DIC=9.8,DIC(0)="AEQFM",DIC("A")="PICK A ROUTINE WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N XPDNAME,D,YES,I S XPDNAME=$P($G(^DIC(9.8,D0,0)),U) Q:XPDNAME="" 
 N D0 F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  S YES=0 D  I YES S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 .F I="INI","INIT","PRE","PRET" I U_$G(^XPD(9.6,D,I))[(U_XPDNAME) S YES=1 Q
 .I $D(^XPD(9.6,D,"KRN",9.8,"NM","B",XPDNAME)) S YES=1
 Q
 ;
 ;
 ;
RPC ;
 S DIC=8994,DIC(0)="AEQM",DIC("A")="PICK A REMOTE PROCEDURE CALL WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N RPCNAME,D S RPCNAME=$P($G(^XWB(8994,D0,0)),U) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",8994,"NM","B",RPCNAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
 ;
 ;
PARAM ;
 S DIC=8989.51,DIC(0)="AEQM",DIC("A")="PICK A PARAMETER DEFINITION WHOSE BUILDS YOU WANT TO SEE: "
 D ^DIC Q:Y'>0  S D0=+Y
 S DICMX="W !,X S Y=$P(^(0),U,4) X ^DD(""DD"") W ?40,Y"
 N PARNAME,D S PARNAME=$P($G(^XTV(8989.51,D0,0)),U) F D=0:0 S D=$O(^XPD(9.6,D)) Q:'D  I $D(^(D,"KRN",8989.51,"NM","B",PARNAME)) N D0 S D0=D,X=$P(^XPD(9.6,D,0),U) X DICMX Q:'$D(D)
 Q
